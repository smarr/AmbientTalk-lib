import ~.<<.<<.linkedlist;
import /.at.lang.futures;
enableFutures(false);

def LinkedListDistributionTestModule := object: {

	def LinkedListDistributionTest := extend: /.at.unit.test.UnitTest.new("LinkedListDistributionTest") with: {
	
		def [a, b, c] := [nil, nil, nil];
		def [A,B] := [nil, nil];
		
		def setUp() {
			def vt[3] { LinkedList.new() };
			[a, b, c] := vt;
		};
		
		def tearDown() {
			[a, b, c].each: { |v| v.clear() };
		};
		
		def testIsIsolate() {
			self.assertTrue(is: a taggedAs: /.at.lang.types.Isolate);
		};
		
		def testAsyncSerialisation() {
			def [fut, res] := makeFuture();
			 
			def A := actor: {
				import /.at.collections.java.linkedlist;
				def o := object: { def m := 41 };
				def i := isolate: { def n := 42 };
				def v := LinkedList.new([1, "foo", i, o]);
				export: v as: LinkedListT;
			};
			when: LinkedListT discovered: { |rv|
				try: {
					self.assertFalse(is: rv taggedAs: /.at.lang.types.FarReference);
					self.assertEquals(rv.removeFirst(), 1);
					self.assertEquals(rv.removeFirst(), "foo");
					self.assertEquals(rv.removeFirst().n, 42);
					self.assertTrue(is: rv.removeFirst() taggedAs: /.at.lang.types.FarReference);
					self.assertTrue(rv.isEmpty());
					res.resolve(true);
				} catch: /.at.lang.types.Exception using: { |e|
					res.ruin(e);
				};
			};
			fut;
		};
		
	};
	
};

LinkedListDistributionTestModule;
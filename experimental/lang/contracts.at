/**
 * AmbientTalk/2 Project
 * (c) Programming Technology Lab, 2006 - 2010
 * Authors: Dries Harnie & Christophe Scholliers
 * 
 * Permission is hereby granted, free of charge, to any person
 * obtaining a copy of this software and associated documentation
 * files (the "Software"), to deal in the Software without
 * restriction, including without limitation the rights to use,
 * copy, modify, merge, publish, distribute, sublicense, and/or
 * sell copies of the Software, and to permit persons to whom the
 * Software is furnished to do so, subject to the following
 * conditions:
 *
 * The above copyright notice and this permission notice shall be
 * included in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
 * EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
 * OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
 * HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
 * WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 * FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
 * OTHER DEALINGS IN THE SOFTWARE.
 */
 
 /* Implementation of ambient contracts, as written in the CEDI2010 paper. */

import /.at.lang.structuraltypes;
import /.at.discovery.discovery;
import /.experimental.lang.AspectMirror;
import /.experimental.lang.DispatchingMirror;
import /.at.lang.futures;
enableFutures(false);

deftype ContractField <: /.at.lang.types.Field;

deftype Marker;

/* Alias for protocolOf. */
def role: obj {
	protocol: obj;
};

/* Rebinding strategies when a field disconnects. There are three choices:
 *   - Frail makes the composition fail if a service annotated with this keyword disconnects;
 *   - WeakRebind denotes that the composition pauses until an equivalent service is discovered;
 *   - SturdyRebind works like WeakRebind, but only resumes if the original service reconnects.
 */
def RebindStrategies := object: {
	def Frail := object: {
		def discover(field, contract, object) {
			field.addObj(object);
		};
	
		def disconnect(field, contract, object) {
			if: field.selected.contains(object) then: {
				contract.fail();
			}
		};
		
		def reconnect(field, contract, object) {
			// do nothing
		};
	};
	
	def WeakRebind := object: {
		def disconnected;
			
		def init() { 
			disconnected := jlobby.java.util.HashSet.new;
			self;
		};
		
		def discover(field, contract, object) {
			if: (disconnected.contains(object)) then: {
				reconnect(field, contract, object);
			} else: {
				field.addObj(object);
			};
		};
		
		def disconnect(field, contract, object) {
			if: (field.selected.contains(object)) then: {
				field.selected.remove(object);
				disconnected.add(object);
				if: !(field.checkSatisfied()) then: {
					contract.pause();
				};
			};
		};
		
		def reconnect(field, contract, object) {
			if: (disconnected.contains(object)) then: {
				field.selected.add(object);
				checkSatisfied();
			};
		};
	};
	
	def SturdyRebind := object: {
		def disconnected;
	
		def init() { 
			disconnected := jlobby.java.util.HashSet.new;
			self;
		};
		
		def discover(field, contract, object) {
			if: ((disconnected.isEmpty).and: { !field.isSatisfied() }) then: {
				field.addObj(object);
			} else: {
				if: (disconnected.contains(object)) then: {
					reconnect(field, contract, object);
				}
			}
		};
		
		def disconnect(field, contract, object) {
			if: (field.selected.contains(object)) then: {
				disconnected.add(object);
				contract.pause();
			};
		};
		
		def reconnect(field, contract, object) {
			if: (disconnected.contains(object)) then: {
				disconnected.remove(object);
				if: (disconnected.isEmpty()) then: {
					//contract.unpause();
				}
			}
		};
	};
};

/* Fields are part of a contract. They keep track of the devices discovered and
 * signal the contract once they are satisfied. Once satisfied, disconnections will
 * consult the rebind strategy to either wait for a reconnection, fail, or wait
 * for a device that also implements the role. See rebinding strategies above.
 */
def makeContractField(maxAmount, role) {
	object: {
		// The name of the field
		def name;
		
		def contract;
		
		def rebind;
		
		// The devices found in the environment that conform to the given role
		def found :=  /.at.collections.java.hashset.HashSet.new();
		
		// Keep track of the amount of state invariants every potential role candidate satisifies. 
		def stateMap :=  /.at.collections.java.hashmap.HashMap.new();
		
		// The devices actually selected to take part in the contract
		def selected :=  /.at.collections.java.hashset.HashSet.new();
		
		// The participants already added to the contract.
		def joined := /.at.collections.java.hashset.HashSet.new();
		
		// Stopping the contract also halts all ongoing role subscriptions
		// (when:discovered: returns a canceler object)
		def cancelers := [];
		
		// List of messages to send to each new object that enters the selected set.
		def invariantsToSend := [];
				
		// List of messages to send to each new object that enters the selected set.
		def stateInvariants := [];		
				
		// TODO: return the selected set (with a DispatchingMirror installed) instead of self.
		def readField() { self };
		
		// Writing to the field adds the object to the contract
		def writeField(obj) { addObj(obj) };
		
		def new(newHost) { self };
		def accessor() { (reflect: self).grabMethod(`readField) };
		def mutator() { (reflect: self).grabMethod(`writeField) };
		
		// TODO: check invariants
		def checkSatisfied() {
			def ret := isSatisfied();
			if: ret then: {
				contract.fieldSatisfied(self)
			};
			ret
		};
		
		// Tell participants that all their base are belong to us.
		def joinContract() {
			def msg := <-joinContract(contract)@ContractMetaMessage;
			selected.each: {|s| if: !(joined.contains(s)) then: { s <+ msg; joined.add(s); } };
		};
		
		// Returns true if the contract is "full".
		def isSatisfied() {
			def amount := selected.size;
			(maxAmount == nil).or: { amount == maxAmount };
		};
		
		// If we can still add more to selected, do so.
		// TODO: checkSatisfied can trigger applyInvariants, which will apply the aspects twice on the given object
		def addObj(obj) {
			if: ((maxAmount == nil).or: {selected.size < maxAmount})  then: { // no limit
				def wasSatisfied := isSatisfied();
				selected.add(obj);
				if: (checkSatisfied().and: { wasSatisfied }) then: {
					invariantsToSend.each: {|msg| obj <+ msg };
				}
			}
		};
		
		// Install a handler that is called on contract side before the participant(s) executes "msgName".
		// The block receives three arguments:
		// * methodName The name of the method called
		// * args The arguments
		// * cancel A canceler object (with a "cancel" method) that prevents the sending of messages.
		// TODO: figure out proper way to undo arg bindings made by block.
		def beforeCallBack: msgName  do: block {
			def advice := advice: { |msgName, args, cancel| block.applyInScope([msgName, args, cancel], clone: contract); };
			installCallback(`beforeCallBack, msgName, advice);
		};
		
		// Install a handler that is called on contract side after the participant(s) has executed "msgName".
		// The block receives five arguments:
		// * methodName The name of the method called
		// * args The arguments
		// * cancel A canceler object (with a "cancel" method) that prevents the sending of messages.
		// * sentMessages A table of [receiver, message] pairs sent by the method.
		// * return The return value of the method.
		def afterCallBack: msgName  do: block {
			def advice := advice: { |msgName, args, cancel, sentMessages, return| block.applyInScope([msgName, args, cancel, sentMessages, return], clone: contract); };
			installCallback(`afterCallBack, msgName, advice);
		};
		
		// Convenience function that combines the above functions.
		def onCallBack: msgName pre: pre post: post {
			beforeCallBack: msgName do: pre;
			afterCallBack:  msgName do: post;
		};
		
		/* A state invariant. It works by hooking into calls like `field:= in participants of the contract.
         * Whenever the field is assigned to, the invariant object is added to or removed from the stateMap
         * set in the enclosing field. Upon installation and reconnection the field is queried.
         */
		def addStateInvariant(field, value) {
			def parentField := self;
			def stateInvariant := object: {
			    def callback(m, args, obj) {
					def set := stateMap.get(obj);
					def satisfied := (set.size == stateInvariants.length );
					
					if: (args[1] == value ) then: {
						set.add(self);
						if: ( !satisfied.and: { set.size == stateInvariants.length } ) then: {
							found.add(obj);
							rebind.discover(parentField, contract, obj);
						}
					} else: {
						set.remove(self);
						if: satisfied then: {
							found.remove(obj);
							rebind.disconnect(parentField, contract, obj);	
						}
					}
				};
				
				def msg := eval: `(<-#field()@[/.at.lang.futures.FutureMessage]) in: self;
				def setter := (field.text+":=");
				
				def installOn(obj) {
					// TODO canceler
					system.println("installing callback for state invariant on " + field);
					on: obj before: {|methodName,arguments| methodName == setter } CallBack: { |m, args, cancel| callback(m, args, obj) };
					
					system.println("checking state invariant on " + field + " now");
					check(obj);
				};
				
				def check(obj) {
					when: obj <+ msg becomes: { |response| callback(field, [response], obj) };
				};
			};
			stateInvariants := stateInvariants + [stateInvariant];
		};

		def installCallback(selector, msgName, advice) {
			def pointcut := script: {|mn, a| msgName.text == mn; };
			
			def msg := <-dummy( pointcut, advice, contract)@[ContractMetaMessage,/.at.lang.futures.FutureMessage];
			msg.selector := selector;
			
			system.println("Installing callback");
			selected.each:{ |service| service <+ msg };
			invariantsToSend := invariantsToSend + [msg];
		};
		
		def applyInvariants(invariants) {
			if: (invariantsToSend == []) then: {
				invariants.applyInScope([], self);
			};
		};

		def startDiscovering() {
			if: (cancelers == []) then: { // Can only start discovery once
			    system.println("starting discovery for " + name);
			    checkSatisfied();
			    
				def c := whenever: role discovered: { |obj|
															
					if: (stateInvariants.length > 0) then: {
						stateMap.put(obj, /.at.collections.java.hashset.HashSet.new());
						stateInvariants.each: { |inv| inv.installOn(obj) };
					} else: {
						found.add(obj);
						rebind.discover(self, contract, obj);
					};
						
					def d := whenever: obj disconnected: {
						found.remove(obj);
						stateMap.get(obj).clear();
						rebind.disconnect(self, contract, obj);
					};
					
					def r := whenever: obj reconnected: {
						//found.add(obj);
						stateInvariants.each: { |inv| inv.check(obj) };
						//rebind.reconnect(self, contract, obj);
					};
					
					cancelers := [d, r];
				};
				cancelers := [c] + cancelers;
			};
		};
		
		def stopDiscovering() {
			cancelers.each: {|c| c.cancel() };
			cancelers := [];
			invariantsToSend := [];
		};
		
	}  taggedAs: [ ContractField ] mirroredBy: { |base| makeDispatchingMirror(base, {base.selected}, role); };
};


// MAPPING OPERATORS
def one(role) {
	makeContractField(1,role); 
};

def many(role) {
	makeContractField(nil,role); 
};

def exactly(amount, role) {
	makeContractField(amount, role); 
};

// REBINDING STRATEGIES
def Frail: participant {
	participant.rebind := RebindStrategies.Frail.new;
	participant;
};

def WeakRebind: participant {
	participant.rebind := RebindStrategies.WeakRebind.new;
	participant;
};

def SturdyRebind: participant {
	participant.rebind := RebindStrategies.SturdyRebind.new;
	participant;
};

// INVARIANTS
def stateInvariant: block on: participant {

};



def contract: block {

	def fields := [];

	def contract := object: {
		def satisfied := jlobby.java.util.HashSet.new;
	
		def contract := self;
		def invariants := [];
		
		def invariant: block on: participant {
			invariants := invariants + [[participant,block]];		
		};
		
		def stateInvariant: field on: participant equals: value {
			/* block := eval: `{ { afterCallBack:  `#(jlobby.edu.vub.at.objects.natives.grammar.AGAssignmentSymbol.jAlloc("`"+field.text + ":="))  do: { |methodName,args|
			//block := eval: `{ { afterCallBack:  `#(jlobby.edu.vub.at.objects.natives.grammar.AGAssignmentSymbol.jAlloc("`"+field.text + ":="))  do: { |methodName,args|

				system.println("Intercepted remote assignment of:  " + "field to :" + args);
			}; } } in: {}.context.lexicalScope;
			
			invariants := invariants + [[participant, block ]];*/
			participant.addStateInvariant(field, value);
		};
		
		def start() {
			system.println("contract started");
			fields.each: {|f| f.startDiscovering() };
		};
		
		def stop() {
			system.println("contract stopped");
			fields.each: {|f| f.stopDiscovering() };
		};
		
		def fail() {
			system.println("contract failed");
			stop();
		};
		
		def pause() {
			system.println("contract paused");
		};
		
		def contractSatisfied() {
			system.println("contract satisfied");
			invariants.each:{|i| i[1].applyInvariants(i[2]) };
			fields.each: {|f| f.joinContract() };
		};
		
		def fieldSatisfied(f) {
			satisfied.add(f);
			system.println("field " + f.name + " satisfied");
			if: (fields.length() == satisfied.size() ) then: {
				contractSatisfied();
			};
			
		};
		
		def canExecute(messages, sender) {
			messages.each: {|m| m[1] := m[1].unpackUsingClassLoader(jlobby.edu.rit.m2mi.M2MI.getClassLoader()) };
			system.println("*** canExecute called: " + [sender, messages]);
			
			false;
		};
		
	};
	
	// TODO inject contract stuff into scope
	def scope := block.context.lexicalScope;
	(reflect: scope).listFields.each: { |f| 
		if: (f.name != `super) then: {
			(reflect: contract).addField(f);
		};
	};
		
	block.applyInScope([], contract);
	
	(reflect: contract).listFields.each: { |f|
		if: (is: f.readField taggedAs: ContractField) then: {
			def realName := f.name;
			def contractField := f.readField();
			contractField.name := realName;
			contractField.contract := contract;
			system.println("adding field " + realName + " to contract");
			(reflect: contract).removeSlot(f.name);
			(reflect: contract).addField(contractField);
			fields := fields + [contractField];
		}
	};	
	contract;
};

self;